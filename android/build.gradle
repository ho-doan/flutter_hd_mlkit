group 'com.hodoan.barcode_scan_custom'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.7.10'
    ext.protobuf_version = '0.8.18'
    ext.protocVersion = '3.11.0'
    ext.javaliteVersion = '3.11.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobuf_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.hodoan.barcode_scan_custom'
    }

    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        main.proto.srcDirs += '../protos'
    }

    defaultConfig {
        minSdkVersion 19
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    dependencies {
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.mockito:mockito-core:5.0.0'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        if (osdetector.os == "osx") {
            artifact = "com.google.protobuf:protoc:$protocVersion:osx-x86_64"
        } else {
            artifact = "com.google.protobuf:protoc:$protocVersion"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.protobuf:protobuf-javalite:$javaliteVersion"

    implementation 'com.google.mlkit:barcode-scanning:17.2.0'
    implementation "androidx.camera:camera-camera2:1.3.2"
    implementation "androidx.camera:camera-view:1.4.0-alpha04"
    implementation 'androidx.camera:camera-lifecycle:1.3.2'
    implementation 'androidx.lifecycle:lifecycle-service:2.7.0'
}